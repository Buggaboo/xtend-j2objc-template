// Xtend to Java step

buildscript {scriptHandler->
    apply from: '../../Xtendroid/repositories.gradle', to: scriptHandler
    apply from: '../../Xtendroid/dependencies.gradle'
    dependencies {
        classpath(project.ext.build.xtend_android_gradle)
        classpath(project.ext.build.xtext_gradle)
        classpath(project.ext.build.android_gradle)
    }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

repositories {
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'org.xtext.android.xtend'
apply from: '../../Xtendroid/dependencies.gradle'


dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile(project.ext.lib.xtend)
  compile(project.ext.lib.android.support_v4)
  compile(project.ext.lib.android.appcompat_v7)
  compile 'io.mikael:urlbuilder:2.0.5'
  compile project(':Xtendroid') // for @AndroidJsonized

  // testing
  androidTestCompile 'com.android.support:support-annotations:23.1.1'
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

  testCompile 'junit:junit:4.12'
}

android {
  compileSdkVersion(project.ext.compileSdkVersion)
  buildToolsVersion(project.ext.buildToolsVersion)

  sourceSets {
    main {
      manifest { srcFile 'AndroidManifest.xml' }
      java.srcDirs = ['src/main/java']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }

    String sharedTestDir = 'src/test/java'
    test {
      java.srcDirs = [ sharedTestDir ]
    }
    androidTest {
      java.srcDirs = [ sharedTestDir ]
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
      release {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
    }

    lintOptions {
      abortOnError false
    }
  }

  // issue #195331
  packagingOptions {
    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
  }
}

/**
Add copy task to the mix (add the copy task after executing the build task)
make the :app:build rely on a completed build by :xtendshared...
but they can't share the same gradle ... since j2objc supports gradle-2.3,
and xtend-android supports gradle-2.10

That's also why I can't do the following to ':shared':
sourceSets.main.java.srcDirs += [ '../xtendshared/build/generated/source/xtend/release' ]
Because then I have to make ':Xtendroid' compile under gradle-2.3.
*/
project.afterEvaluate {
  project.tasks.create(name: 'copy2shared', type: Copy, dependsOn: 'generateReleaseXtext') {
    from './build/generated/source/xtend/release'
    into '../shared/src/main/java'
    include '**/*.java'
  }

  project.tasks['assemble'].dependsOn += ['copy2shared']

  project.tasks.create(name: 'copyTests2shared', type: Copy, dependsOn: 'generateDebugAndroidTestXtext') {
    from './build/generated/source/xtend/debugAndroidTest'
    into '../shared/src/test/java'
    include '**/*.java'
  }

  project.tasks['assembleDebugAndroidTest'].dependsOn += ['copyTests2shared']

}
